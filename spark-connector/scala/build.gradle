apply plugin: 'scala'

if (project.property('version').equals('unspecified')){
    version '1.2.1'
} else{
    version project.property('version')
}

project.ext.mcsapi_dependency_version = version

sourceSets {
    main {
        scala {
            srcDirs = ['src/main/scala']
        }
    }
    test {
        scala {
            srcDirs = ['src/test/scala']
        }
    }
}

repositories{
	jcenter()
	flatDir {
		dirs "${project.projectDir}/../../java/build/libs/"
	}
}

dependencies{
	compile name: "javamcsapi-${project.ext.mcsapi_dependency_version}"
	compile 'org.scala-lang:scala-library:2.11.12'
	compile 'org.apache.spark:spark-sql_2.11:2.2.1'
	testCompile 'junit:junit:4.12'
	testCompile 'org.mariadb.jdbc:mariadb-java-client:2.2.0'
}

test {
        def default_lib_path = "${project.projectDir}/../../java"
        systemProperty "java.library.path","${project.hasProperty('java.library.path') ? project.property('java.library.path') : default_lib_path}"
        systemProperty "spark.driver.memory","5g"
}

task benchmark(type: JavaExec, dependsOn: classes){
        main = 'com.mariadb.columnstore.api.connector.Benchmark'
        jvmArgs = ['-Xms4g', '-Xmx10g']
        systemProperty "java.library.path","${project.projectDir}/../../java"
        systemProperty "spark.driver.memory","10g"
        classpath sourceSets.test.runtimeClasspath
        classpath configurations.runtime
}

compileJava.dependsOn 'copyFiles'

task copyFiles(type: Copy) {
    from "${project.projectDir}/../python/mcsapi_reserved_words.txt"
    into "${project.projectDir}/src/main/resources/com/mariadb/columnstore/api/connector/resources"
}
